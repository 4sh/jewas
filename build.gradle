apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'sonar'
apply plugin: 'maven'

sourceCompatibility = 1.7
targetCompatibility = 1.7

def timestamp = new java.text.SimpleDateFormat("yyyyMMddHHmmss").format(new Date());
// For cobertura
def cobSerFile = "${project.buildDir}/reports/tests/cobertura.ser"
def srcOriginal = "${sourceSets.main.classesDir}"
def srcCopy = "${srcOriginal}-copy"
// System property to activate test coverage during test execution
// (de-activated by default)
def COVERAGE_SYS_PROPERTY = "test.coverage.activated"
// Set this to false to generate a war file
// TODO: implement the war file related build ?
// Useless property (for the moment...)
def STANDALONE_PACKAGING = true
// Root directory on demo environnment, where will be hosted every targetEnvironnment directories
def ENV_DEMO_DEPLOY_ROOT_PATH = "/home/bbeeg/dist";
// Demo connection properties
def ENV_DEMO_HOST = '91.121.39.83'
def ENV_DEMO_USER = "bbeeg"
def ENV_DEMO_PASS = "4SHbbfr"
// System property used to define target execution environnment used for deployment
// See ENV_PROPERTIES map for the possible values to set in this system property
def TARGET_ENV_SYS_PROPERTY = "deploy.target.env"
// System property used to skip upload of artefact during start* tasks
def SKIP_UPLOAD_SYS_PROPERTY = "deploy.skip.upload"
// System property used to define an XDebug java port to launch remote app in debug mode
def DEBUG_PORT_SYS_PROPERTY = "deploy.debug.port"
// System property used to start NewRelic monitoring on app if needed
def NEW_RELIC_MONITORING_SYS_PROPERTY = "monitor.with.new.relic"
def NEW_RELIC_AGENT_PATH = "/home/bbeeg/tools/newrelic/newrelic.jar"

if(!hasProperty("releasesRepoUsername")
        || !hasProperty("releasesRepoPassword")){
    ant.fail "You should define a ~/.gradle/gradle.properties containing properties 'releasesRepoUsername' and 'releasesRepoPassword' to access 4SH releases repository !"
}

org.apache.ivy.util.url.CredentialsStore.INSTANCE.addCredentials("Artifactory Realm", "repo.4sh.fr", releasesRepoUsername, releasesRepoPassword);


def ENV_PROPERTIES = [
   nightly: [
           targetDir: ENV_DEMO_DEPLOY_ROOT_PATH+"/nightly",
           bbeegPort: 18086,
           visioEegRootUrl: "http://nightly.visio.bbeeg.4sh.fr",
           visioEegInternalUrl: "http://127.0.0.1:18087",
           cachedStaticResourcesRootDirectory: ENV_DEMO_DEPLOY_ROOT_PATH+"/nightly/bbeeg_cachedStaticResources",
           contentFileRepository: ENV_DEMO_DEPLOY_ROOT_PATH+"/nightly"+"/contents/",
           tmpContentFileRepository: ENV_DEMO_DEPLOY_ROOT_PATH+"/nightly"+"/tmp/",
           h2ServerPort: 18082,
           h2DbPath: ENV_DEMO_DEPLOY_ROOT_PATH+"/nightly/bbeegDB",
           elasticSearchAdress: "127.0.0.1",
           elasticSearchPort: 19200,
           specificJavaOpts: "-Xmx256m"
   ], qa: [
           targetDir: ENV_DEMO_DEPLOY_ROOT_PATH+"/qa",
           bbeegPort: 28086,
           visioEegRootUrl: "http://qa.visio.bbeeg.4sh.fr",
           visioEegInternalUrl: "http://127.0.0.1:28087",
           cachedStaticResourcesRootDirectory: ENV_DEMO_DEPLOY_ROOT_PATH+"/qa/bbeeg_cachedStaticResources",
           contentFileRepository: ENV_DEMO_DEPLOY_ROOT_PATH+"/qa"+"/contents/",
           tmpContentFileRepository: ENV_DEMO_DEPLOY_ROOT_PATH+"/qa"+"/tmp/",
           h2ServerPort: 28082,
           h2DbPath: ENV_DEMO_DEPLOY_ROOT_PATH+"/qa/bbeegDB",
           elasticSearchAdress: "127.0.0.1",
           elasticSearchPort: 29200,
           specificJavaOpts: "-Xmx512m"
   ]
]

group = "fr.4sh.bbeeg"
def versionBase = "0.1"

// handles releases gracefully
gradle.taskGraph.whenReady {taskGraph ->
    if (taskGraph.hasTask(':release')) {
        version = versionBase
        versionForManifest = version
    } else {
        version = versionBase + '-SNAPSHOT'
        versionForManifest = versionBase + "-" + timestamp
    }
    jar {
      manifest {
        attributes 'Main-Class': 'fr.fsh.bbeeg.Main'
        attributes 'Implementation-Title': 'BB-EEG'
        attributes 'Implementation-Version': versionForManifest
        attributes 'Implementation-Vendor': '4SH France'
      }
    }
}

repositories {
    // looking into maven local repo only if localRepo system property is set to true
    // Will be useful to test snapshot deps (like jewas) which are not yet deployed on artifactory...
    if(System.properties["localRepo"] == true){
        mavenLocal()
    }
    mavenRepo(urls: "http://repo.4sh.fr/libs-release/")
    mavenRepo(urls: "http://repo.4sh.fr/libs-snapshot/")
    mavenRepo urls: ["http://oss.sonatype.org/content/repositories/releases/"]
}

configurations {
    intTestCompile { extendsFrom testCompile }
    intTestRuntime { extendsFrom intTestCompile, runtime }
    ssh
}

dependencies {
    compile(
            [group: 'com.h2database', name: 'h2', version: '1.3.158'],
            [group: 'fr.4sh.jewas', name: 'jewas', version: '0.1-SNAPSHOT', transitive: true],
            [group: 'com.beust', name:'jcommander', version: '1.17', transitive: true],
            [group: 'commons-dbcp', name: 'commons-dbcp', version: '1.4'],
            [group: 'org.elasticsearch', name: 'elasticsearch', version: '0.17.6'],
            [group: 'org.elasticsearch', name: 'elasticsearch-mapper-attachments', version: '0.17.6'],
            [group: 'com.jayway.restassured', name: 'rest-assured', version: '1.3']
    )
    testCompile(
            [group: 'junit', name: 'junit', version: '4.8.2', transitive: true]
    )
    testRuntime 'net.sourceforge.cobertura:cobertura:1.9.4.1'
    ssh 'org.apache.ant:ant-jsch:1.7.0'
}

sourceSets {
    intTest {
        compileClasspath = sourceSets.main.classes + configurations.intTestCompile
        runtimeClasspath = classes + sourceSets.main.classes + configurations.intTestRuntime
        java {
            srcDir "src/integration-test/java"
        }
        resources {
            srcDir "src/integration-test/resources"
        }
    }
}

// Allows to generate USER_HOME idea variable uniformely on every OS
ideaModule {
    variables.put("USER_HOME", file(System.getProperty("user.home")))
}

if(STANDALONE_PACKAGING){
    jar {
      // Convert every jars in a fatjar
      from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    }

    sourceSets{
        main {
            resources {
                srcDir "src/main/java"
                srcDir "src/main/webapp"
            }
        }
    }
}

task checkPropsForDeployment << {
    if(System.properties[TARGET_ENV_SYS_PROPERTY] == null
            || !ENV_PROPERTIES.containsKey(System.properties[TARGET_ENV_SYS_PROPERTY])){
        throw new IllegalArgumentException("You should define <"+TARGET_ENV_SYS_PROPERTY+"> system property !")
    }
}

task initSsh << {
    ant.taskdef(name: 'scp2',
    	classname: 'org.apache.tools.ant.taskdefs.optional.ssh.Scp',
    	classpath: configurations.ssh.asPath)
    ant.taskdef(name: 'ssh',
    	classname: 'org.apache.tools.ant.taskdefs.optional.ssh.SSHExec',
    	classpath: configurations.ssh.asPath)
}

task uploadOnDemo(dependsOn: [initSsh, checkPropsForDeployment, test, build]) << {
    if(!"true".equals(System.properties[SKIP_UPLOAD_SYS_PROPERTY])){
        def deployDirectory = ENV_PROPERTIES[System.properties[TARGET_ENV_SYS_PROPERTY]].targetDir

        println "Deploying BB-EEG to $deployDirectory"

        def libFiles = file(libsDir).listFiles().findAll { it =~ "\\.jar\$" }
        libFiles.each { File f ->
            println "Uploading ${f.name} (${f.size()/1000000} Mo) ..."

            ant.scp2(file: f,
                    remoteTofile: "${ENV_DEMO_USER}:${ENV_DEMO_PASS}@${ENV_DEMO_HOST}:${deployDirectory}/bbeeg.jar",
                    port: 2224,
                    trust: true,
                    verbose: true
            )
        }
    } else {
        println "Upload of BB-EEG skipped !"
    }
}

task bootstrapElasticSearch() << {
    def deployDirectory = ENV_PROPERTIES[System.properties[TARGET_ENV_SYS_PROPERTY]].targetDir

    // Installing needed plugins
    ant.ssh(host: ENV_DEMO_HOST, username: ENV_DEMO_USER, password: ENV_DEMO_PASS, port: 2224,
		command: """${deployDirectory}/elasticsearch/bin/plugin \
		     install mapper-attachments""",
        trust: true, failonerror: true)
}

task startElasticSearch(dependsOn: [initSsh, checkPropsForDeployment]) << {
    def deployDirectory = ENV_PROPERTIES[System.properties[TARGET_ENV_SYS_PROPERTY]].targetDir
    def executionPort = ENV_PROPERTIES[System.properties[TARGET_ENV_SYS_PROPERTY]].elasticSearchPort

    ant.ssh(host: ENV_DEMO_HOST, username: ENV_DEMO_USER, password: ENV_DEMO_PASS, port: 2224,
		command: """kill -9 \$(cat "${deployDirectory}/elasticsearch.pid")""", trust: true, failonerror: false)

    // Waiting 5s after kill of elastic search
    Object.sleep(5000)

    //ant.ssh(host: ENV_DEMO_HOST, username: ENV_DEMO_USER, password: ENV_DEMO_PASS, port: 2224,
    //        command: """rm -R ${deployDirectory}/elasticsearch/data""", trust: true, failonerror: false)

    // Waiting 5s after remove data folder of elastic search
    //Object.sleep(5000)

    ant.ssh(host: ENV_DEMO_HOST, username: ENV_DEMO_USER, password: ENV_DEMO_PASS, port: 2224,
		command: """${deployDirectory}/elasticsearch/bin/elasticsearch \
		    -f -Des.transport.tcp.port=${executionPort} -Des.index.store.type=memory -Xms512m -Xmx512m  \
		    > "${deployDirectory}/elasticsearch.log" 2> "${deployDirectory}/elasticsearch.err.log" \
		    & echo \$! > "${deployDirectory}/elasticsearch.pid" """,
        trust: true, failonerror: true)
    
    // Waiting 5s after start of elastic search
    Object.sleep(5000)
}

task startBBEEG(dependsOn: [initSsh, checkPropsForDeployment, startElasticSearch]) << {
    def targetEnv = ENV_PROPERTIES[System.properties[TARGET_ENV_SYS_PROPERTY]]
    def deployDirectory = targetEnv.targetDir
    def executionPort = targetEnv.bbeegPort
    def visioRootUrl = targetEnv.visioEegRootUrl
    def visioEegInternalUrl = targetEnv.visioEegInternalUrl
    def contentFileRepository = targetEnv.contentFileRepository
    def tmpContentFileRepository = targetEnv.tmpContentFileRepository
    def h2ServerPort = targetEnv.h2ServerPort
    def h2DbPath = targetEnv.h2DbPath
    def elasticSearchPort = targetEnv.elasticSearchPort
    def elasticSearchAdress = targetEnv.elasticSearchAdress
    def cachedStaticResourcesRootDirectory = targetEnv.cachedStaticResourcesRootDirectory
    def specificJavaOpts = targetEnv.specificJavaOpts

    def javaOpts = "-Xms256m -Dfile.encoding=UTF-8 ${specificJavaOpts} "

    if(System.properties[DEBUG_PORT_SYS_PROPERTY] != null
        && !"".equals(System.properties[DEBUG_PORT_SYS_PROPERTY])){
        javaOpts += " -Xdebug -Xrunjdwp:transport=dt_socket,server=y,suspend=n,address="+System.properties[DEBUG_PORT_SYS_PROPERTY]+" -Xnoagent -Djava.compiler=NONE"
    }

    if(System.properties[NEW_RELIC_MONITORING_SYS_PROPERTY] != null
        && "true".equals(System.properties[NEW_RELIC_MONITORING_SYS_PROPERTY])){
        javaOpts += " -javaagent:"+NEW_RELIC_AGENT_PATH
    }

    ant.ssh(host: ENV_DEMO_HOST, username: ENV_DEMO_USER, password: ENV_DEMO_PASS, port: 2224,
		command: """kill -9 \$(cat "${deployDirectory}/bbeeg.pid")""", trust: true, failonerror: false)
    ant.ssh(host: ENV_DEMO_HOST, username: ENV_DEMO_USER, password: ENV_DEMO_PASS, port: 2224,
		command: """rm -Rf ${cachedStaticResourcesRootDirectory}""", trust: true, failonerror: false)
    ant.ssh(host: ENV_DEMO_HOST, username: ENV_DEMO_USER, password: ENV_DEMO_PASS, port: 2224,
		command: """mkdir ${cachedStaticResourcesRootDirectory}""", trust: true, failonerror: true)
    // TODO: pass the http port here...
    ant.ssh(host: ENV_DEMO_HOST, username: ENV_DEMO_USER, password: ENV_DEMO_PASS, port: 2224,
		command: """/usr/lib/jvm/jdk1.7.0/jre/bin/java ${javaOpts} -jar "${deployDirectory}/bbeeg.jar" \
		    -httpPort ${executionPort} -visioRootUrl ${visioRootUrl} -visioEegInternalUrl ${visioEegInternalUrl} \
		    -contentFileRepository ${contentFileRepository} -tmpContentFileRepository ${tmpContentFileRepository} -cachedStaticResourcesRootDirectory ${cachedStaticResourcesRootDirectory} \
		    -h2ServerPort ${h2ServerPort} -h2DbPath ${h2DbPath} \
		    -elasticSearchAdress ${elasticSearchAdress} -elasticSearchPort ${elasticSearchPort} \
		    > "${deployDirectory}/bbeeg.log" 2> "${deployDirectory}/bbeeg.err.log" \
		    & echo \$! > "${deployDirectory}/bbeeg.pid" """,
        trust: true, failonerror: true)
}

task intTest(type: Test) {
    testClassesDir = sourceSets.intTest.classesDir
    classpath = sourceSets.intTest.runtimeClasspath
}

task initializeIntTestContext << {
    ant.echo "TODO: unzip & start elastic search here !"
}
intTestClasses.dependsOn initializeIntTestContext

// For cobertura ...
if ("true".equals(System.getProperty(COVERAGE_SYS_PROPERTY))) {
    gradle.taskGraph.beforeTask { task ->
        if (task == test) {
            ant {
                // delete data file for cobertura, otherwise coverage would be added
                delete(file: cobSerFile, failonerror: false)
                // delete copy of original classes
                delete(dir: srcCopy, failonerror: false)
                // import cobertura task, so it is available in the script
                taskdef(resource: 'tasks.properties', classpath: configurations.testRuntime.asPath)
                // create copy (backup) of original class files
                copy(todir: srcCopy) {
                    fileset(dir: srcOriginal)
                }
                // instrument the relevant classes in-place
                'cobertura-instrument'(datafile: cobSerFile) {
                    fileset(dir: srcOriginal,
                            includes: "**/*.class")
                }
            }
        }
    }

    gradle.taskGraph.afterTask { task ->
        if (task == test && new File(srcCopy).exists()) {
            // replace instrumented classes with backup copy again
            ant {
                delete(file: srcOriginal)
                move(file: srcCopy,
                        tofile: srcOriginal)
            }
            // create cobertura reports
            ant.'cobertura-report'(destdir: "${project.buildDirName}/reports/tests/",
                    format: 'xml', srcdir: "src/main/java", datafile: cobSerFile)
        }
    }

    test {
        ignoreFailures = true
        systemProperties["net.sourceforge.cobertura.datafile"] = cobSerFile
    }
}

sonar {
    serverUrl = "http://sonar.4sh.fr"

    globalProperty "sonar.jdbc.url", "jdbc:mysql://sonar:3306/sonar"
    globalProperty "sonar.jdbc.driverClassName", "com.mysql.jdbc.Driver"
    globalProperty "sonar.jdbc.username", "sonar"
    globalProperty "sonar.jdbc.password", "4SHsoar"

    globalProperty "sonar.java.source", "1.7"
    globalProperty "sonar.java.target", "1.7"
}