apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'sonar'
apply plugin: 'maven'
// For gradle release
apply from: "https://launchpad.net/gradle-release/trunk/1.0pre/+download/apply.groovy"

// For cobertura
def cobSerFile = "${project.buildDir}/reports/tests/cobertura.ser"
def srcOriginal = "${sourceSets.main.classesDir}"
def srcCopy = "${srcOriginal}-copy"
def COVERAGE_SYS_PROPERTY = "test.coverage.activated"
// Sysprop allowing to ignore test failures during the build
def IGNORE_TEST_FAILURES_SYS_PROPERTY = "test.ignore.failures";

if(!hasProperty("releasesRepoUsername")
        || !hasProperty("releasesRepoPassword")){
    ant.fail "You should define a ~/.gradle/gradle.properties containing properties 'releasesRepoUsername' and 'releasesRepoPassword' to access 4SH releases repository !"
}

group = "fr.4sh.jewas"
archivesBaseName = "jewas"

sourceCompatibility = 1.7
targetCompatibility = 1.7

if(System.getProperty("jdk") != null){
    sourceCompatibility = System.getProperty("jdk")
    targetCompatibility = System.getProperty("jdk")
}

test {
    ignoreFailures = "true".equalsIgnoreCase(System.getProperty(IGNORE_TEST_FAILURES_SYS_PROPERTY))
}

configurations {
   deployerJars
}

repositories {
    maven {
        credentials {
            username releasesRepoUsername
            password releasesRepoPassword
        }
        url "http://repo.4sh.fr/libs/"
        artifactUrls "http://repo.4sh.fr/libs/"
    }
}

dependencies {
    compile(
            [group: "freemarker", name: 'freemarker', version: '2.3.9', transitive: true],
            [group: 'com.google.code.gson', name: 'gson', version: '1.7.1', transitive: true],
            [group: 'joda-time', name: 'joda-time', version: '1.6.2', transitive: true],
            [group: "org.jboss.netty", name: 'netty', version: '4.0.0.Alpha0-4sh', transitive: true],
            [group: 'org.slf4j', name: 'slf4j-api', version: '1.6.4', transitive: true]
    )
    runtime(
            [group: 'ch.qos.logback', name: 'logback-classic', version: '1.0.0']
    )
    testCompile(
            [group: 'junit', name: 'junit', version: '4.8.2', transitive: true],
            [group: 'com.jayway.restassured', name: 'rest-assured', version: '1.3'],
            [group: 'commons-dbcp', name: 'commons-dbcp', version: '1.4'],
            [group: 'commons-io', name: 'commons-io', version: '1.4'],
            [group: 'com.h2database', name: 'h2', version: '1.3.158'],
            [group: 'org.codehaus.groovy', name: 'groovy', version: '1.7.10']
            // For multipart file upload
            //[group: 'org.apache.httpcomponents', name: 'httpmime', version: '4.0.3']
    )
    testRuntime 'net.sourceforge.cobertura:cobertura:1.9.4.1'
    testRuntime 'fr.4sh.jewas.tests:resourcesInJarForTests:1.0'

    deployerJars "org.apache.maven.wagon:wagon-http:1.0-beta-2"
}
task packageJavadoc(type: Jar, dependsOn:javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task packageSources(type: Jar) {
   from sourceSets.main.allSource
   classifier = 'sources'
}
artifacts {
   archives(packageJavadoc) {
      type = 'javadoc'
      extension = 'jar'
   }
   archives(packageSources)
}

uploadArchives {
    repositories.mavenDeployer {
        configuration = configurations.deployerJars
        snapshotRepository(url: "http://repo.4sh.fr/libs-snapshot-local") {
            authentication(userName: releasesRepoUsername, password: releasesRepoPassword)
        }
        repository(url: "http://repo.4sh.fr/libs-release-local/") {
            authentication(userName: releasesRepoUsername, password: releasesRepoPassword)
        }
    }
}

release {
    // Hack allowing to skip the "next version" prompt
    project.setProperty("gradle.release.useAutomaticVersion", "true");
    // Define here custom commit messages
    newVersionCommitMessage = "Re-snapshoted project to version "
    preTagCommitMessage = "Preparing version for release "
    tagCommitMessage = "Tag for version "
}
createReleaseTag.dependsOn uploadArchives

// Allows to generate USER_HOME idea variable uniformely on every OS
ideaModule {
    variables.put("USER_HOME", file(System.getProperty("user.home")))
}

// For cobertura ...
if ("true".equals(System.getProperty(COVERAGE_SYS_PROPERTY))) {
    gradle.taskGraph.beforeTask { task ->
        if (task == test) {
            ant {
                // delete data file for cobertura, otherwise coverage would be added
                delete(file: cobSerFile, failonerror: false)
                // delete copy of original classes
                delete(dir: srcCopy, failonerror: false)
                // import cobertura task, so it is available in the script
                taskdef(resource: 'tasks.properties', classpath: configurations.testRuntime.asPath)
                // create copy (backup) of original class files
                copy(todir: srcCopy) {
                    fileset(dir: srcOriginal)
                }
                // instrument the relevant classes in-place
                'cobertura-instrument'(datafile: cobSerFile) {
                    fileset(dir: srcOriginal,
                            includes: "**/*.class")
                }
            }
        }
    }

    gradle.taskGraph.afterTask { task ->
        if (task == test && new File(srcCopy).exists()) {
            // replace instrumented classes with backup copy again
            ant {
                delete(file: srcOriginal)
                move(file: srcCopy,
                        tofile: srcOriginal)
            }
            // create cobertura reports
            ant.'cobertura-report'(destdir: "${project.buildDirName}/reports/tests/",
                    format: 'xml', srcdir: "src/main/java", datafile: cobSerFile)
        }
    }

    test {
        ignoreFailures = true
        systemProperties["net.sourceforge.cobertura.datafile"] = cobSerFile
    }
}

/*sonar {
    serverUrl = "http://sonar.4sh.fr"

    globalProperty "sonar.jdbc.url", "jdbc:mysql://sonar:3306/sonar"
    globalProperty "sonar.jdbc.driverClassName", "com.mysql.jdbc.Driver"
    globalProperty "sonar.jdbc.username", "sonar"
    globalProperty "sonar.jdbc.password", "4SHsoar"

    globalProperty "sonar.java.source", "1.6"
    globalProperty "sonar.java.target", "1.6"
}*/
